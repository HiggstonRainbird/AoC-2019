(* ::Package:: *)

(* ::Text:: *)
(*Written December 13th, 2019.*)


(* ::Subsubsection:: *)
(*Import*)


(* ::Input:: *)
(*input=StringSplit[#," => "]&/@Flatten[Import[FileNameJoin[{NotebookDirectory[],"Day14Input.txt"}],"List"]];*)


(* ::Subsubsection:: *)
(*Sample*)


(* ::Subsubsection:: *)
(*Part 1*)


(* ::Input:: *)
(*reactants=StringCases[#,n:DigitCharacter..~~" "~~r:LetterCharacter..:>{ToExpression[n],r}]&/@input[[;;,1]];*)
(*products={ToExpression[#[[1]]],#[[2]]}&/@StringSplit[input[[;;,-1]]," "];*)
(*materialList=Union[Flatten[reactants[[;;,;;,2]]],products[[;;,2]]];*)
(*oreCount=0;*)
(**)
(*ClearAll@quantities;*)
(*quantities[s_String]:=0;*)
(**)
(*ClearAll@runReaction*)
(*runReaction[s_String]:=*)
(*Module[{reactantsToGet,productsToOutput},*)
(*reactantsToGet=reactants[[Position[products,s][[1,1]]]];*)
(*productsToOutput=products[[Position[products,s][[1,1]]]];*)
(*Do[*)
(*If[*)
(*r[[2]]=="ORE",oreCount+=r[[1]],*)
(*While[quantities[r[[2]]]<r[[1]],*)
(*runReaction[r[[2]]];*)
(*];*)
(*quantities[r[[2]]]-=r[[1]];*)
(*]*)
(*,{r,reactantsToGet}];*)
(*quantities[s]+=productsToOutput[[1]];*)
(*];*)
(*runReaction["FUEL"];*)
(*oreCount*)


(* ::Subsubsection:: *)
(*Part 2*)


(* ::Input:: *)
(*reactants=StringCases[#,n:DigitCharacter..~~" "~~r:LetterCharacter..:>{ToExpression[n],r}]&/@input[[;;,1]];*)
(*products={ToExpression[#[[1]]],#[[2]]}&/@StringSplit[input[[;;,-1]]," "];*)
(**)
(*g=Graph[Flatten[Table[#->products[[i,2]]&/@reactants[[i,;;,2]],{i,Length[products]}]]];*)
(*order=Reverse[TopologicalSort[g]];*)
(**)
(*ClearAll@requested;*)
(*Do[requested[o]=0,{o,order}];*)
(*requestReaction[s_String,n_]:=*)
(*Module[{reactantsToGet,productsToOutput},*)
(*reactantsToGet=reactants[[Position[products,s][[1,1]]]];*)
(*productsToOutput=products[[Position[products,s][[1,1]]]];*)
(*Do[*)
(*requested[r[[2]]]+=r[[1]]*Ceiling[n/productsToOutput[[1]]];*)
(*,{r,reactantsToGet}];*)
(*];*)
(*AbsoluteTiming[*)
(*hi=2Ceiling[10^12/857266];*)
(*lo=Ceiling[10^12/857266];*)
(*mid=lo+Round@((hi-lo)/2);*)
(*While[*)
(*hi-lo>1,*)
(**)
(*Do[requested[o]=0,{o,order}];*)
(*requested["FUEL"]=mid;*)
(*Do[*)
(*If[requested[o]!=0,*)
(*requestReaction[o,requested[o]];*)
(*requested[o]=0;*)
(*];*)
(*,{o,order[[;;-2]]}];*)
(**)
(*If[*)
(*requested["ORE"]>10^12,*)
(*hi=mid,*)
(*lo=mid*)
(*];*)
(*mid=lo+Round@((hi-lo)/2)];*)
(*lo]*)
